buildscript {
  repositories {
    maven {
      url 'https://artifactory.wetransform.to/artifactory/libs-snapshot-local'
    }
  }
  dependencies {
    classpath 'to.wetransform.hale:gradle-hale-plugin:1.2.0-SNAPSHOT'
  }
}

apply plugin: 'to.wetransform.hale'

repositories {
  // mavenLocal() // for testing
}

hale {
  cliVersion = '4.0.0'
}

afterEvaluate {
  if (!project.hasProperty('haleCliExecutable')) {
    // add schemabuilder reader as dependency
    dependencies {
      hale "eu.esdihumboldt.hale:eu.esdihumboldt.hale.io.schemabuilder:${hale.cliVersion}"
    }
  }
}

configurations.all {
  resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

def allTask = task('buildSchemas') {
  group 'Schema'
  description "Build all schemas"
}

def schemasDir = file('schemas')
schemasDir.eachDir { dir ->
  def name = dir.name

  def buildTask = task("buildSchema-$name", type: hale.cli()) {
    args << 'schema'
    args << 'rewrite'

    args << '--source'
    args << new File(dir, 'schema.groovy').absolutePath

    args << '--target'
    args << new File(dir, 'schema.hsd.json')
    args << '--target-writer'
    args << 'eu.esdihumboldt.hale.common.schema.persist.hsd.json.write'

    logToConsole = true

    description "Builds the $name schema file from the schema builder definition."
    group 'Schema'
  }

  allTask.dependsOn(buildTask)
}

defaultTasks 'buildSchemas'

wrapper {
  gradleVersion = '6.9.2' // Note: Version 7 is not compatible with hale plugin
}
